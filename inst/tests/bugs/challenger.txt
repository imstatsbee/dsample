## Version 1.

model
{
  # Space Shuttle Challenger

  # likelihood
  for(i in 1 : N) {
  y[i] ~ dbern(p[i])
  logit(p[i]) <- beta0 + beta1 * z[i] }
  for(i in 1:N){
    z[i] <- step(x[i] - K)
  }

  #priors
  K ~ dunif(53, 81)
  beta0 ~ dnorm(0.0, 0.01)
  beta1 ~ dnorm(0.0, 0.01)
}


## Version 2.

model {
 for(i in 1:K) {
 distress[i] ~ dbin(p[i], 6)
 # Regression model by Dalal et al with temp and pressure
 logit(p[i]) <- a + b*temp[i] + c*press[i]
 distress.rep[i] ~ dbin(p[i], 6) # Replicate values for model validation
 diff.obs[i] <- pow(distress[i] - 6*p[i], 2)/(6*p[i]*(1-p[i]))
 diff.rep[i] <- pow(distress.rep[i] - 6*p[i], 2)/(6*p[i]*(1-p[i]))
 }
chisq.obs <- sum(diff.obs[])
chisq.rep <- sum(diff.rep[])
p.value <- step(chisq.rep - chisq.obs)
distress.31 ~ dbin(p.31, 6) # Predicted number of distress events for launch 61-L
logit(p.31) <- a + b*31 + c*200 # Regression model with temp and pressure
 # from day of the accident
# Prior distributions
a ~ dnorm(0, 0.000001)
b ~ dnorm(0, 0.000001)
c ~ dnorm(0, 0.000001)
}
data
list(
 distress=c(0,1,0,0,0,0,0,0,1,1,1,0,0,2,0,0,0,0,0,0,2,0,1),
 temp=c(66,70,69,68,67,72,73,70,57,63,70,78,67,53,67,75,70,81,76,79,75,76,58),

press=c(50,50,50,50,50,50,100,100,200,200,200,200,200,200,200,200,200,200,200,2
00,200,200,200),
 K=23
 )
inits
list(a=5, b=0, c=0)
list(a=1, b=-0.1, c=0.1)

